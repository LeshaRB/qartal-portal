plugins {
  id "net.ltgt.apt" version "0.15"
}

group "com.nomis"
version "1.0-SNAPSHOT"

apply from: "$rootDir/gradle/idea.gradle"
apply from: "$rootDir/gradle/versions.gradle"
apply from: "$rootDir/gradle/options.gradle"

allprojects {
  apply plugin: "net.ltgt.apt"
  apply plugin: "net.ltgt.apt-idea"

  apply from: "$rootDir/gradle/java.gradle"
  apply from: "$rootDir/gradle/codeQuality.gradle"
}

subprojects {
  repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
  }

  dependencies {
    // Test
    testCompile("junit:junit:$junitVersion")
  }

  task fastRun(dependsOn: [clean, build]) {
  }

  gradle.taskGraph.whenReady {
    if (gradle.taskGraph.hasTask(":${project.name}:fastRun")) {
      checkstyleMain.enabled = false
      checkstyleTest.enabled = false
      findbugsMain.enabled = false
      findbugsTest.enabled = false
      pmdMain.enabled = false
      pmdTest.enabled = false
      test.enabled = false
    }
  }
}